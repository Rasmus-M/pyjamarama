*********************************************************************
*
* Game loop for Invaders game room
*
invaders_loop:
       bl   @vsync_and_sound
       bl   @wally_draw
       bl   @wally_handler
       bl   @wally_step_check
       bl   @invaders_update
       bl   @invaders_update
       bl   @invaders_update
       bl   @vsync_and_sound
       bl   @invaders_update
       bl   @invaders_update
       bl   @invaders_update
       bl   @invaders_update_explosions
       bl   @invaders_exit_check
       bl   @invaders_bullet_update
       bl   @invaders_update_bombs
       bl   @wally_check_for_death
       bl   @upload_sprites
       bl   @sfx_player
       inc  @frame
       bl   @game_exit_check
       mov  r0,r0
       jeq  invaders_loop
       b    @main
*// invaders_loop

*********************************************************************
*
* Initialise invaders
*
invaders_init:
       .proc
       mov  @invaders_type_ptr,r0
       ai   r0,-invaders_first_type
       srl  r0,1
       ai   r0,>1d
       mov  r0,@wally_room_no
       bl   @screen_clear_room
       bl   @draw_ceiling_wall_floor
       li   r0,>0f00
       li   r1,LDC31
       bl   @furniture_draw
       li   r0,invaders_data
       mov  r0,@invaders_data_ptr
invaders_init_0:
       mov  @invaders_data_ptr,r0
       mov  *r0+,r1
       mov  r0,@invaders_data_ptr
       cb   r1,@byte_fe
       jeq  invaders_init_0
       ci   r1,>ffff
       jeq  invaders_init_1
       movb r1,@bitmap_x+1
       movb @r1lb,@bitmap_y+1
       mov  @invaders_current_frame,r0
       bl   @bitmap_16_draw
       jmp  invaders_init_0
invaders_init_1:
       li   r0,invaders_data
       mov  r0,@invaders_data_ptr
*      Reset explosions
       li   r5,invaders_explosion_slots
invaders_init_2:
       mov  *r5,r0
       ci   r0,>ffff
       jeq  invaders_init_3
       clr  *r5
       ai   r5,4
       jmp  invaders_init_2
invaders_init_3:
*      Reset bombs
       li   r5,invaders_bomb_data
invaders_init_4:
       mov  *r5,r0
       ci   r0,>ffff
       jeq  invaders_init_5
       clr  *r5
       ai   r5,4
       jmp  invaders_init_4
invaders_init_5:
       bl   @deallocate_sprites
       .endproc
*// invaders_init

*********************************************************************
*
* Initialise invaders x positions
*
invaders_init_x_pos:
       li   r0,invaders_data           ; Start with top row, left hand column.
       li   r1,>1000                   ; Left hand position.
       li   r2,8                       ; 8 columns.
invaders_init_x_pos_0:
       movb r1,*r0                     ; Top.
       inct r0
       movb r1,*r0                     ; Middle.
       inct r0
       movb r1,*r0                     ; Bottom.
       inct r0
       ai   r1,>1800                   ; Next column position.
       dec  r2
       jne  invaders_init_x_pos_0      ; Next column.
       clr  @invaders_change_dir_flag
       li   r0,1                       ; Reset direction to right.
       mov  r0,@invaders_current_dir
       li   r0,>0018
       mov  r0,@invaders_left
       rt
*// invaders_init_x_pos

*********************************************************************
*
* Initialise invaders y positions
*
invaders_init_y_pos:
       li   r0,invaders_data+1	       ; Start with top row, left hand column for 8 columns.
       li   r1,>3800
       li   r3,>5800
       li   r4,>7800
       li   r2,8
invaders_init_y_pos_0:
       movb r1,*r0                     ; Top.
       inct r0
       movb r3,*r0                     ; Middle.
       inct r0
       movb r4,*r0                     ; Bottom.
       inct r0
       dec  r2
       jne  invaders_init_y_pos_0      ; Next column.
       rt
*// invaders_init_y_pos

*********************************************************************
*
* Draw invaders, drop bombs
*
invaders_update:
       .proc
       mov  @invaders_data_ptr,r4      ; Start of data.
invaders_update_0:
       mov  *r4+,r1                    ; Get x and y
       cb   r1,@byte_fe                ; See if valid
       jeq  invaders_update_0          ; If not valid, move onto next invader.
invaders_update_1:
       ci   r1,>ffff                   ; Check for end of data.
       jne  invaders_update_3          ; If not the go on to handle invader.
*      End of data
       li   r4,invaders_data           ; Reset start of first invader.
       mov  r4,@invaders_data_ptr
       mov  @invaders_current_frame,r0 ; Update frame to the next one.
       mov  r0,@invaders_next_frame
       inc  r0                         ; Update to next frame.
       c    r0,@invaders_end_frame     ; See if frame has reached end frame.
       jne  invaders_update_2
       mov  @invaders_start_frame,r0   ; Reset to start if so.
invaders_update_2:
       mov  r0,@invaders_current_frame ; Store updated frame.
       mov  @invaders_change_dir_flag,r0
       sla  r0,1
       mov  r0,@invaders_y_offset
       jeq  invaders_update_11         ; Return
       clr  @invaders_change_dir_flag  ; Reset flag.
       neg  @invaders_current_dir      ; Change direction.
       jmp  invaders_update_11         ; Return
*      Not end of data
invaders_update_3:
       mov  r4,@invaders_data_ptr      ; Set pointer to next invader.
       dect r4                         ; Push address back to current invader.
       movb r1,@bitmap_x+1             ; Set draw to position.
       movb @r1lb,@bitmap_y+1
       ab   @invaders_current_dir+1,r1 ; Get direction and update X position in direction.
       cb   r1,@byte_e8                ; Check for right hand end of screen reached.
       jne  invaders_update_5
invaders_update_4:
       li   r0,1                       ; If reached, change direction for all invaders.
       mov  r0,@invaders_change_dir_flag
       jmp  invaders_update_6
invaders_update_5:
       cb   r1,@byte_10                ; Check for left hand end of screen reached.
       jeq  invaders_update_4          ; Change direction if so.
invaders_update_6:
       a    @invaders_y_offset,r1      ; Add offset to y
       cb   @r1lb,@byte_a0             ; Reached bottom?
       jl   invaders_update_7
       movb r13,@death_flag       ; Die
invaders_update_7:
       mov  r1,*r4                     ; Save updated position
*      Draw invader
       .push r1
       bl   @bitmap_16_erase            ; Erase invader from old position
       .peek r1
       mov  @invaders_next_frame,r0
       movb r1,@bitmap_x+1             ; Draw invader at new position and new frame.
       movb @r1lb,@bitmap_y+1
       bl   @bitmap_16_draw
       .pop r1
       swpb r1
       .push r1
       li   r0,2                       ; Check for hitting Wally.
       bl   @wally_hit_test
*      Manage bombs
       bl   @rand                      ; Create a random number.
       .pop r4                         ; Position
       ci   r0,>0800                   ; Test random number
       jhe  invaders_update_11         ; Return
*      Drop a bomb.
       li   r5,invaders_bomb_data
invaders_update_8:
       mov  *r5,r6                     ; Get sprite
       jeq  invaders_update_9          ; Found empty slot
       ci   r6,>ffff                   ; End marker
       jeq  invaders_update_11         ; Return
       ai   r5,4
       jmp  invaders_update_8
*      Bomb, empty slot found.
invaders_update_9:
       mov  @invaders_bomb_frame,r0    ; Get the next bitmap within the range of ($c0 ~ $c6)
       inc  r0
       ci   r0,>00c7
       jl   invaders_update_10
       li   r0,>00c0
invaders_update_10:
       mov  r0,@invaders_bomb_frame
       bl   @get_sprite_pattern
       mov  r1,r3
       movb @byte_07,@r3lb
       bl   @allocate_sprite
       mov  r0,*r5
       jeq  invaders_update_11
       mov  r4,*r0+
       mov  r3,*r0
       bl   @play_bomb
invaders_update_11:
       .endproc
*// invaders_update

*********************************************************************
*
* Update explosions in the Arcade Room
*
invaders_update_explosions:
       .proc
       li   r5,invaders_explosion_slots
invaders_update_explosions_0:
       mov  *r5,r4
       jeq  invaders_update_explosions_1
       ci   r4,>ffff
       jeq  invaders_update_explosions_2
       mov  @2(r5),r0                  ; Get animationn frame
       inc  r0
       ci   r0,>00a5
       jeq  invaders_update_explosions_3
       mov  r0,@2(r5)
       bl   @get_sprite_pattern
       movb r1,@sprite_pattern(r4)
       jmp  invaders_update_explosions_1
invaders_update_explosions_3:
       mov  r4,r0
       bl   @deallocate_sprite
       clr  *r5
invaders_update_explosions_1:
       ai   r5,4
       jmp  invaders_update_explosions_0
invaders_update_explosions_2:
       .endproc
*// invaders_update_explosions

*********************************************************************
*
* Check to see if Wally has left the arcade room
*
invaders_exit_check:
       .proc
       movb @wally_x,r0                ; See if Wally is at the far left of the room.
       sb   @byte_09,r0
       cb   r0,@byte_fe
       jl   invaders_exit_check_1      ; Do nothing if not.
       li   r0,>0015
       mov  r0,@wally_room_no          ; Move into the next room and update.
       li   r0,>98d0
       mov  r0,@wally_y
       li   r0,sfx_leave_room
       li   r1,2
       bl   @play_sfx_and_wait
       bl   @room_enter
       li   r10,stack+4                ; Reset stack
       szcb r14,@is_arcade_mode
       b    @game_loop                 ; Go back to normal game play.
invaders_exit_check_1:
       .endproc
*// invaders_exit_check

*********************************************************************
*
* Invaders: Bullet Update
*
invaders_bullet_update:
       .proc
       mov  @invaders_bullet_sprite,r4 ; See if a bullet exists, if not then do nothing.
       jeq  invaders_bullet_update_4
       li   r7,invaders_data           ; Run through the data of each invader.
       li   r6,>18
invaders_bullet_update_1:
       mov  *r7+,r1                    ; Get x and y
       cb   r1,@byte_fe                ; Only do anything if invader is active.
       jne  invaders_bullet_update_3   ; If it is check for collision.
invaders_bullet_update_2:
       dec  r6
       jne  invaders_bullet_update_1   ; Move onto next invaders.
*      Update bullet
       sb   @byte_03,*r4               ; Decrease Y pos by three pixels.
       cb   *r4,@byte_30               ; See if at top of screen.
       jhe  invaders_bullet_update_4   ; Return
*      Disable bullet
       mov  r4,r0
       bl   @deallocate_sprite
       clr  @invaders_bullet_sprite
       jmp  invaders_bullet_update_4   ; Return
*      Check collision with invader
invaders_bullet_update_3:
       mov  r1,r0                      ; Copy position of invader
       swpb r0
       sb   *r4,r0                     ; Check against Y position of bullet.
       sb   @byte_10,r0
       cb   r0,@byte_e0
       jl   invaders_bullet_update_2   ; Next invader if not.
       swpb r0
       sb   @1(r4),r0                  ; Check against X position of bullet.
       sb   @byte_10,r0
       cb   r0,@byte_e0
       jl   invaders_bullet_update_2   ; Next invader if not.
*      Hit
       mov  r4,r0
       bl   @deallocate_sprite
       clr  @invaders_bullet_sprite    ; Set bullet as disabled.
       dect r7                         ; Back to current invader
       movb @byte_fe,*r7               ; Set invader as disabled as well.
       movb r1,@bitmap_x+1
       movb @r1lb,@bitmap_y+1
       .push r1
       bl   @bitmap_16_erase           ; Erase invader
       .pop r1
       bl   @invaders_create_explosion
       dec  @invaders_left
       jne  invaders_bullet_update_4
       bl   @invaders_next_stage
invaders_bullet_update_4:
       .endproc
*// invaders_bullet_update

*********************************************************************
*
* Update and draw bombs for the Arcade room
*
invaders_update_bombs:
       .proc
       li   r7,invaders_bomb_data
invaders_update_bombs_1:
       mov  *r7,r6                     ; Get sprite
       jeq  invaders_update_bombs_2
       ci   r6,>ffff
       jeq  invaders_update_bombs_4
       mov  *r6,r1                     ; Get y and x
       ab   @byte_02,r1
       cb   r1,@byte_a8
       jhe  invaders_update_bombs_3
       mov  r1,*r6
       li   r0,2
       bl   @wally_hit_test
       jmp  invaders_update_bombs_2
invaders_update_bombs_3:
       swpb r1
       bl   @invaders_create_explosion
       mov  r6,r0
       bl   @deallocate_sprite
       clr  *r7
invaders_update_bombs_2:
       ai   r7,4
       jmp  invaders_update_bombs_1
invaders_update_bombs_4:
       .endproc
*// invaders_update_bombs

*********************************************************************
*
* Create a 'bullet' if required
*
wally_shoot_create:
       mov  @invaders_bullet_sprite,r0 ; See if a 'bullet' already exists.
       jne  wally_shoot_create_1
       li   r0,>00bf                   ; Knife and Fork
       bl   @get_sprite_pattern
       movb @byte_0e,@r1lb
       mov  r1,r3
       mov  @wally_y,r4
       sb   @byte_08,r4
       bl   @allocate_sprite           ; If not, create one by placing it just above Wally.
       mov  r0,@invaders_bullet_sprite
       jeq  wally_shoot_create_1
       mov  r4,*r0+
       mov  r3,*r0
       bl   @play_shot
wally_shoot_create_1:
       b    @wally_handler_5
*// wally_shoot_create

*********************************************************************
*
* Create an explosion in the Arcade room
*
* r1: position x MSB, y LSB
*
invaders_create_explosion:
       .proc
       mov  r1,r4
       li   r5,invaders_explosion_slots
invaders_create_explosion_0:
       mov  *r5,r0
       jeq  invaders_create_explosion_1
       ci   r0,>ffff
       jeq  invaders_create_explosion_2
       ai   r5,4
       jmp  invaders_create_explosion_0
invaders_create_explosion_1:
       li   r0,>009f                   ; Bitmap frame
       mov  r0,@2(r5)                  ; Store it
       bl   @get_sprite_pattern
       mov  r1,r3                      ; Pattern
       movb @byte_0b,@r3lb             ; Color yellow
       bl   @allocate_sprite
       mov  r0,*r5
       jeq  invaders_create_explosion_2
       swpb r4                         ; Swap x and y
       mov  r4,*r0+                    ; Set sprite position
       mov  r3,*r0                     ; set sprite pattern and color
       bl   @play_explosion
invaders_create_explosion_2:
       .endproc
*// invaders_create_explosion

*********************************************************************
*
* Reset everything for next stage.
*
invaders_next_stage:
       .proc
       mov  @invaders_type_ptr,r0      ; See if reached the last stage.
       cb   *r0,r14                    ; If not then reset invaders for next stage.
       jne  invaders_next_stage_1
       mov  @wally_lives,r0
       ci   r0,3                       ; Cap the max number of lives, player ca only have upto three lives.
       jeq  invaders_next_stage_0
       inc  r0                         ; Increase the number of lives and update on screen.
       mov  r0,@wally_lives
       bl   @life_draw
invaders_next_stage_0:
       li   r0,invaders_first_type     ; Go back to first stage again.
invaders_next_stage_1:
       movb *r0+,r1                    ; Reset invaders to current stage / animations.
       movb r1,@invaders_current_frame+1
       movb r1,@invaders_start_frame+1
       ab   r13,r1
       movb r1,@invaders_next_frame+1
       movb *r0+,@invaders_end_frame+1 ; Reset invaders to current stage / animations.
       mov  r0,@invaders_type_ptr      ; Point to the next stage ready for next time level is cleared.
       bl   @invaders_init_x_pos
       bl   @invaders_init_y_pos
       bl   @invaders_init
       .endproc
*// invaders_next_stage

*********************************************************************

invaders_data_ptr:
       data invaders_data              ; Points to invader data.
invaders_type_ptr:
       data invaders_first_type+2      ; Points to address holding the current type of invader.
invaders_left:
       data >0018
invaders_current_frame:
       data >0075                      ; Current bitmap frame of invaders.
invaders_next_frame:
       data >0076                      ; Next bitmap frame of invaders.
invaders_start_frame:
       data >0075                      ; Start frame of animation.
invaders_end_frame:
       data >007a                      ; End frame of animation.
invaders_current_dir:
       data >0001                      ; Current direction of invaders.
invaders_change_dir_flag:
       data >0000                      ; When set indicates change of direction.
invaders_y_offset:
       data >0000                      ; Invaders offset from top.
invaders_bomb_frame:
       data >00c0                      ; F45D

invaders_first_type:
       byte >75,>7a                    ; Venus Flytrap
       byte >7B,>7F                    ; Roast Chicken
       byte >93,>99                    ; Angel of Death
       byte >99,>9B                    ; Space Invader
       byte >AD,>B3                    ; Spooky Hand
       byte >B3,>BB                    ; Spinning Axe
       byte >BB,>BF                    ; Ghost
       byte >FF,>FF                    ; End of data.

invaders_data:
       byte >0A,>38                    ; Column 1 [X pos, Y pos]
       byte >0A,>58
       byte >0A,>78
       byte >22,>38                    ; Column 2..
       byte >22,>58
       byte >22,>78
       byte >3A,>38
       byte >3A,>58
       byte >3A,>78
       byte >52,>38
       byte >52,>58
       byte >52,>78
       byte >6A,>38
       byte >6A,>58
       byte >6A,>78
       byte >82,>38
       byte >82,>58
       byte >82,>78
       byte >9A,>38
       byte >9A,>58
       byte >9A,>78
       byte >B2,>38                    ; Column 8
       byte >B2,>58
       byte >B2,>78
       byte >FF,>FF                    ; End of data.

invaders_explosion_slots:
       data >0000,>0000                ; Sprite (0=empty), Frame
       data >0000,>0000
       data >0000,>0000
       data >0000,>0000
       data >0000,>0000
       data >0000,>0000
       data >ffff                      ; End of data.

invaders_bomb_data:
       data >0000,>0000                ; Sprite (0=empty), Frame
       data >0000,>0000
       data >0000,>0000
       data >ffff                      ; End of data.

invaders_bullet_sprite:
       data >0000
